buildscript {
    repositories {
        jcenter()
        maven {
            name 'forge'
            url 'http://files.minecraftforge.net/maven'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
plugins {
    id 'java'
    id 'org.ajoberstar.grgit' version '3.1.1'
}

apply plugin: 'net.minecraftforge.gradle'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext {
    revision = grgit.log().size()
    hash = grgit.head().abbreviatedId
}
version += ".$revision"
if (release != 'RELEASE')
    version += "-$release"

minecraft {
    mappings channel: mappings_channel, version: mappings_version
    accessTransformer file("src/main/resources/META-INF/tabbychat_at.cfg")
    runs {
        client {
            workingDirectory file('run')

            properties('forge.logging.markers': 'SCAN', 'forge.logging.console.level': 'info')

            mods {
                tabbychat {
                    source sourceSets.main
                }
            }
            ideaModule = 'TabbyChat-2.main'
        }
    }
}
repositories {
    jcenter()
}
dependencies {
    minecraft "net.minecraftforge:forge:$forge_version"
    implementation 'net.sf.jazzy:jazzy:0.5.2-rtext-1.4.1-2'
    testImplementation 'junit:junit:4.12'
}
afterEvaluate {
    println '==========================================='
    println "   TabbyChat $version ($hash)"
    println '==========================================='
}
manifest {
    attributes(
            'Specification-Title': "tabbychat",
            'Specification-Vendor': "killjoy1221",
            'Specification-Version': "$version (git ${project.hash}",
            'Implementation-Title': project.name,
            'Implementation-Version': "$version",
            'Implementation-Vendor': "killjoy1221",
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            FMLAT: 'tabbychat_at.cfg'
    )
}
